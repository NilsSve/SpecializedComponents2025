// f_FindFile_Global.pkg

// Find the given name in DFPath. If it exists, return the full name.
Function FindFile Global String sFileName Returns String
    String  sFullName sPath sPathSep sDir sMessage
    Integer iSeparator iSepPos
    Boolean bFileExists
    
    // Only check if there are no sDirectory iSeparators in the name.
    Move "" to sFullName
    Move 0  to iSeparator
    If (sFileName contains (Sysconf(SYSCONF_Dir_Separator))) Begin
        Move 1 to iSeparator
    End
    If (Not(iSeparator)) Begin
        Move 0 to bFileExists
        Move (Sysconf(SYSCONF_Path_Separator)) to sPathSep
        Get_Attribute DF_OPEN_Path to sPath
        If (Length(sPath)) Begin
            Repeat
                Pos sPathSep In sPath to iSepPos
                If iSepPos Begin
                    Left sPath  to sDir (iSepPos - 1)
                    Right sPath to sPath (Length(sPath) - iSepPos)
                    If (Length(sDir)) Begin
                        Append sDir (Sysconf(SYSCONF_Dir_Separator)) sFileName
                        File_Exist sDir bFileExists
                        If bFileExists Begin
                            Move sDir to sFullName
                        End
                    End
                End
            Until ((Not(iSepPos)) Or bFileExists)
            
            If (Not(bFileExists)) Begin
                If (Length(sPath)) Begin
                    Move sPath to sDir
                    Append sDir (Sysconf(SYSCONF_Dir_Separator)) sFileName
                    File_Exist sDir bFileExists
                    If bFileExists Begin
                        Move sDir to sFullName
                    End
                End
            End
        End
        Else Begin
            Move "." to sDir
            Append sDir (Sysconf(SYSCONF_Dir_Separator)) sFileName
            File_Exist sDir bFileExists
            If bFileExists Begin
                Move sDir to sFullName
            End
        End
    End
    Else Begin
        Move sFileName to sFullName
    End
    
    Function_Return sFullName
End_Function